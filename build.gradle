plugins {
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '1.4.0'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
    id 'org.jetbrains.dokka' version '1.4.0-rc'
}

allprojects {
    apply plugin: 'org.jetbrains.dokka'
    apply plugin: 'maven-publish'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'com.github.johnrengelman.shadow'

    shadowJar {
        archiveClassifier.set("")
    }

    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://jitpack.io' }
        maven { url 'https://papermc.io/repo/repository/maven-public/' }
        maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    }

    dependencies {
        compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        compileOnly 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.9'

        testCompile group: 'org.jetbrains.kotlin', name: 'kotlin-test-junit', version: '1.4.0'
    }
}

dokkaHtmlCollector {
    outputDirectory = "$projectDir/docs"
}

def ver = '2.1.2'

group 'dev.jaims'
version ver

subprojects {
    group 'dev.jaims.mcutils'
    version ver

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from (components.java)
            }
        }
        repositories {
            mavenLocal()
            maven {
                def releasesRepoUrl = "https://repo.jaims.dev/repository/maven-releases/"
                def snapshotsRepoUrl = "https://repo.jaims.dev/repository/maven-snapshots/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

                credentials {
                    username = project.property('sonatype.user')
                    password = project.property('sonatype.password')
                }
            }
        }
    }

}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}